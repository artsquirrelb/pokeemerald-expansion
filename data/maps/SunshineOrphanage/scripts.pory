mapscripts SunshineOrphanage_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 0) {
            setflag(FLAG_SPAWN_INVISIBLE)
            setflag(FLAG_HIDE_MIKI)
            setflag(FLAG_HIDE_SHINJIRO)
            setflag(FLAG_HIDE_TALK_BUTTON)
            setflag(FLAG_TEMP_1)
        }
        elif (var(VAR_AKIHIKOS_INTRO_STATE) == 4) {
            setflag(FLAG_HIDE_TALK_BUTTON)
            clearflag(FLAG_HIDE_MIKI)
            clearflag(FLAG_HIDE_SHINJIRO)
        }
        elif (var(VAR_AKIHIKOS_INTRO_STATE) >= 5) {
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 0: SunshineOrphanageOnFrame0
        VAR_AKIHIKOS_INTRO_STATE, 4: SunshineOrphanageOnFrame4
    ]
}

script SunshineOrphanageOnFrame0 {
    goto (SunshineOrphanage_Eventscript_ShowTheMap)}

script SunshineOrphanage_Eventscript_ShowTheMap {
    applymovement(LOCALID_PLAYER, SlowlyShowTheMap)
    waitmovement(0)
    msgbox(format("Akihiko's Prologue: Sunshine Memories\n
    {COLOR GREEN}{CLEAR_TO 64}Level cap: 10"))
    warpsilent(MAP_SUNSHINE_ORPHANAGE_AKIHIKOS_ROOM2F, 5, 4)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 1)
    setvar(VAR_CURRENT_LEVEL_CAP, 10)
}

movement SlowlyShowTheMap {
    walk_up *8
}

script SunshineOrphanageOnFrame4{
    goto (Miki_and_Shinjiro_Notice_Akihiko)
}

script Miki_and_Shinjiro_Notice_Akihiko {
    lockall
    playse (SE_PIN)
    applymovement (3, Miki_Runs_To_Akihiko)
    waitmovement(0)
    msgbox("{SPEAKER NAME_MIKI}There you are!")
    releaseall
    goto(Miki_and_Akihiko_walk_to_Shinjiro)
}

movement Miki_Runs_To_Akihiko {
    emote_exclamation_mark
    delay_16
    walk_fast_right *2
    walk_fast_up
}

script Miki_and_Akihiko_walk_to_Shinjiro {
    lock
    applymovement (3, Miki_leading_Akihiko_to_Shinjiro)
    applymovement (LOCALID_PLAYER, Miki_leading_Akihiko_to_Shinjiro)
    waitmovement(0)
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Miki wants to go to the woods but there are wild Pokemons there.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Shinji, it's fineee. As long as we stay out of the tall grass.\p
    {SPEAKER NAME_MIKI}{DESTROY_MUGSHOT}To the woods! To the woods!\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_DOUBT}Sigh..."))
    closemessage
    release
    goto(Shinjiro_and_Miki_Join_Party)
    }

movement Miki_leading_Akihiko_to_Shinjiro {
    walk_down
    walk_left
}

script Shinjiro_and_Miki_Join_Party {
    lock
    applymovement (4, Shinjiro_joins)
    applymovement (3, Miki_joins)
    waitmovement(0)
    removeobject (4)
    removeobject(3)
    setflag(FLAG_HIDE_MIKI)
    setflag(FLAG_HIDE_SHINJIRO)
    setflag(FLAG_TEMP_1)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 5)
    clearflag(FLAG_HIDE_TALK_BUTTON)
    release
}
movement Shinjiro_joins {
    walk_right
}

movement Miki_joins {
    walk_up
}

script Boy_1_OrphanageEntrance {
    lock
    faceplayer
    setspeaker("Yuta")
    msgbox(format("Can I grow tall like these sunflowers?\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}Just drink lots of milk!"))
    release
}

script Girl_OrphanageEntrance {
    lock
    faceplayer
    setspeaker("Yuko")
    msgbox(format("Sometimes I see people wearing black accompanying a man in a white coat speaking with our teachers.\p 
    I wonder who they are...\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}I asked Ms. Kobayashi once and she said they donated a lot to the orphanage."
    ))
    release
}

script Sunshine_Orphanage_Direction_Sign {
    lock
    msgbox("Sunshine Woods {DOWN_ARROW}\nBeware of wild Pokemons!"
    )
    release
}

script Welcome_To_Sunshine_Orphanage {
    lock
    msgbox (format("Welcome to Sunshine Orphanage!\p
    If you have any questions, feel free to ask the staff at the main hall counter."))
    release
}

script Donors_List {
    lock
    msgbox (format("List of top donors (in alphabet order):
    --- Kai --- Kazuhisa ---\p
    --- Kirijo --- --- ---
    --- Ryota --- --- Takuya"))
    release
}

script Sunflora_NPC {
    lock
    faceplayer
    playmoncry(SPECIES_SUNFLORA, CRY_MODE_NORMAL)
    release
}

script Budew_NPC {
    lock
    faceplayer
    playmoncry(SPECIES_BUDEW, CRY_MODE_NORMAL)
    release
}