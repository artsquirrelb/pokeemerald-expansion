mapscripts SunshineOrphanageMainHall_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 7: SunshineOrphanageMainHall_OnWarp7]
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 7) {
            clearflag(FLAG_HIDE_MIKI)
            clearflag(FLAG_HIDE_SHINJIRO)
            setobjectxyperm (4, 11, 23)
			setobjectmovementtype (4, MOVEMENT_TYPE_FACE_RIGHT)
            hideobjectat(15, MAP_SUNSHINE_ORPHANAGE_MAIN_HALL)}
        if (var(VAR_AKIHIKOS_INTRO_STATE) == 11) {
            clearflag(FLAG_HIDE_SHINJIRO)
            setobjectxyperm (4, 12, 5)
            setobjectmovementtype (4, MOVEMENT_TYPE_FACE_DOWN)
            setobjectxyperm (13, 18, 6)
            setobjectmovementtype (13, MOVEMENT_TYPE_FACE_LEFT)
        }
    }
            
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_AKIHIKOS_INTRO_STATE, 7: SunshineOrphanageMainHall_Eventscript_OnFrame7
        VAR_AKIHIKOS_INTRO_STATE, 11: SunshineOrphanageMainHall_Eventscript_OnFrame11]
}

movement faceright {
    face_right
}

movement faceleft {
    face_left 
}

script SunshineOrphanageMainHall_OnWarp7 {
    applymovement(LOCALID_PLAYER, faceup)
}
script SunshineOrphanageMainHall_Eventscript_OnFrame7 {
    goto (SunshineOrphanageMainHall_Eventscript_HealingPawmi)
}

script SunshineOrphanageMainHall_Eventscript_OnFrame11{
    goto(SunshineOrphanageMainHall_Eventscript_MsKobayashiAskedToGoPickUpAPackage)
}

script SunshineOrphanageMainHall_Eventscript_HealingPawmi {
    lock
    delay(30)
    playse(MUS_HEAL)
    waitse
    release
    goto(SunshineOrphanageMainHall_Eventscript_PawmiIsHealed)
}
movement facedown {
    face_down
}
movement BlisseyHealingPawmi {
    walk_in_place_down
}

script SunshineOrphanageMainHall_Eventscript_PawmiIsHealed {
    lock
    applymovement(11, BlisseyHealingPawmi)
    playmoncry(SPECIES_BLISSEY, CRY_MODE_NORMAL)
    waitmoncry
    delay(30)
    applymovement(LOCALID_PLAYER, facedown)
    applymovement(13, facedown)
    applymovement(14, facedown)
    waitmovement(0)
    delay(16)
    showobjectat (15, MAP_SUNSHINE_ORPHANAGE_MAIN_HALL)
    applymovement(15, PutdownbyAkihiko)
    waitmovement(0)
    msgbox(format(
        "{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}There you go."))
    closemessage
    release
    goto (SunshineOrphanageMainHall_Eventscript_PawmiAppreciated)
}


movement PutdownbyAkihiko {
    walk_down
    face_up
    delay_8
    walk_in_place_up
    delay_8
    walk_in_place_up
}

script SunshineOrphanageMainHall_Eventscript_PawmiAppreciated {
    lock
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 8){
        goto (SunshineOrphanageMainHall_Eventscript_GetPawmiStarter)
    }
    else {  
    playmoncry(SPECIES_PAWMI, CRY_MODE_NORMAL)
    applymovement(15, HeartPawmi)
    waitmoncry
    waitmovement(0)
    delay(30)
    msgbox(format(
        "{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_SMILE}Looks like it wants to stay with you.\p
        {SPEAKER NAME_MIKI}{DESTROY_MUGSHOT}Can we keep it?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}Can we, Ms. Kobayashi?\p
        {SPEAKER NAME_KOBAYASHI}{DESTROY_MUGSHOT}Normally you aren't allowed to have your own Pokemon here.\p However, since you are going
        to leave in a few days, we can make an exception.\p
        Just make sure to not cause trouble in the mean time."))
    closemessage
    applymovement (14, MikiHappy)
    msgbox(format(
        "{SPEAKER NAME_MIKI}Yay!\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}Thank you, Ms. Kobayashi!\p
        {SPEAKER NAME_KOBAYASHI}{DESTROY_MUGSHOT}Here, I'll give you a pokeball to keep it."))
    closemessage
    msgbox("{PLAYER} received 1{COLOR LIGHT_GREEN} (hidden){COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Cherish Ball.")
    closemessage
    msgbox(format("{COLOR LIGHT_GREEN}Tips: The shiny chance is roughly 1/100. \p 
    No Pokemon are shiny locked, including the story starters.\p 
    The story starters have been buffed and come with pre-determined Nature and Perfect IVs.\p
    You also can not release them."))
    setvar(VAR_AKIHIKOS_INTRO_STATE, 8)}
    release
}

script SunshinOrphanageMainHall_Eventscript_ShinjiroTellsYouToCatchPawmi {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Go on, Pawmi is waiting for you."))
    release
}

script SunshineOrphanageMainHall_Eventscript_MikiTellsYouToCatchPawmi {
    lock
    faceplayer
    msgbox(format("{SPEAKER NAME_MIKI}Let's give Pawmi a home, big bro!"))
    release
}

movement HeartPawmi {
    jump_in_place_up
    delay_8
    emote_heart
}

movement MikiHappy {
    emote_music
    jump_in_place_down
}

script SunshineOrphanageMainHall_Eventscript_GetPawmiStarter {
    lockall
    msgbox ("Let Pawmi in the Cherish Ball?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        setflag(FLAG_SYS_POKEMON_GET)
        givemon(SPECIES_PAWMI_DELTA, 5, ITEM_ABILITY_CHANGER, ITEM_CHERISH_BALL, NATURE_ADAMANT, 1, MON_MALE,  0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Received Pawmi.")
            hideobjectat(15, MAP_SUNSHINE_ORPHANAGE_MAIN_HALL)
            call(NicknamePartyMon)
            goto(SunshineOrphanageMainHall_Eventscript_GoToSleep)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Received Pawmi.")
            hideobjectat(15, MAP_SUNSHINE_ORPHANAGE_MAIN_HALL)
            call(NicknameBoxMon)
            goto(SunshineOrphanageMainHall_Eventscript_GoToSleep)
        }
        else{
            msgbox("Party full.")}
    }
    else{
        applymovement (15, PawmiDisappointed)
        msgbox(NewLabTest_NotChosen)
    }
    waitmessage
    releaseall
    end
}

movement PawmiDisappointed {
    shake_horizontal
}

script SunshineOrphanageMainHall_Eventscript_GoToSleep {
    lock
    applymovement (4, TeacherFacingAkihiko)
    waitmovement
    msgbox(format(
        "{SPEAKER NAME_KOBAYASHI}Alright.\p
        I supposed it's quite late now. You all should go to rest.\p
        See you tomorrow."))
    setvar(VAR_AKIHIKOS_INTRO_STATE, 9)
    fadescreenswapbuffers(FADE_TO_BLACK)
    playse(MUS_HEAL)
    waitse
    removeobject(13)
    removeobject(14)
    removeobject(15)
    setflag(FLAG_HIDE_MIKI)
    setflag(FLAG_HIDE_SHINJIRO)
    warpsilent(MAP_BLANK_MAP, 0, 0)
    release
    end
}

movement TeacherFacingAkihiko {
    walk_right
    face_up
}


script Boy_1_MainHall {
    lock
    faceplayer
    setspeaker ("Jin")
    msgbox(format("
    I really like the omelet rice the chef cooks for us."))
    release
}



script Girl_1_MainHall {
    lock
    faceplayer
    setspeaker("Riko")
    msgbox(format(
        "When I graduate and get a job I'll contribute back to our orphanage."))
    release
}

script Boy_2_MainHall {
    lock
    faceplayer
    setspeaker("Yamamoto")
    msgbox(format(
        "They don't allow us children to have Pokemon of our own but we can still play with the staff's Pokemons."))
    release
}

script Girl_2_MainHall {
    lock
    faceplayer
    setspeaker("Hinata")
    msgbox(format(
        "{PLAYER}! I'm also getting adopted next week!\p My future adoptive parents are talking with my teacher right now.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY} That's great! Congrats!"))
    release
}

script RichMan_MainHall {
    lock
    faceplayer
    setspeaker("Wealthy Man")
    msgbox(format(
        "We decided to adopt a daughter because my wife wanted one.\p If she's happy then I'm happy."))
    release
}

script RichLady_MainHall {
    lock
    faceplayer
    setspeaker("Wealthy Woman")
    msgbox(format(
        "We will raise Momo with love and care."))
    release
}

script Teacher_MainHall {
    lock
    setspeaker("Ms. Kobayashi")
    if(var(VAR_AKIHIKOS_INTRO_STATE) < 5){
        faceplayer
        msgbox(format(
        "Oh {PLAYER}, Miki is waiting for you outside with Shinjiro."))}

    elif(var(VAR_AKIHIKOS_INTRO_STATE) == 5 && var(VAR_AKIHIKOS_INTRO_STATE) == 6) {
        faceplayer
        msgbox(format(
        "I'm a little busy right now. Can you go play somewhere else, {PLAYER}?"))
    }
    
    elif(var(VAR_AKIHIKOS_INTRO_STATE) == 10){
        faceplayer
        msgbox("Take care, dear.")
    }
    else {
        faceplayer
        msgbox("Take care of Pawmi, okay dear?")}
    release
}

script Boy_3_MainHall {
    lock
    faceplayer
    setspeaker("Kenichi")
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 4) {
        msgbox(format(
        "Oh hey, {PLAYER}! Miki is waiting for you outside.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE} I know, thanks!"))
    }
    elif (var(VAR_AKIHIKOS_INTRO_STATE) == 5) {msgbox("Stay safe in the woods.\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE} We will!")}
    else {
        msgbox(format(
        "I heard you found a rare Pokemon in the woods?\p
        {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SURPRISED} Pawmi is rare?"))
    }
    release
}

script DiningRoom {
    lock
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_HAPPY}
    It's the dining room. The pancakes here are the best."))
    release
}

script BackyardDoor {
    lock
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}
    Outside is the backyard. We grow crops there."))
    release
}

script Girls_HallwayMainHall {
    lock
    msgbox( format(
        "{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}"
        "It's the way to the girls' hallway. I have no business there right now."))
    release
}

script RestroomMainHall {
    lock
    msgbox(format (
        "{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}"
        "It's the restroom. I'm good for now."
    ))
    release
}

script PokeballShelf {
    lock
    msgbox(format (
        "{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}"
        "Those belong to the staff. I shouldn't touch them."
    ))
    release
}

script Porygon_PC {
    lock
    faceplayer
    playmoncry (SPECIES_PORYGON, CRY_MODE_NORMAL)
    waitmoncry
    if (flag(FLAG_SYS_POKEMON_GET) == TRUE) {
        call(EventScript_AccessPokemonStorage)}

    else {msgbox (format("
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}
    Porygon can access the Pokemon Storage. If only I have a Pokemon."))
    release
    end}
}

script MartLady {
	lock
	faceplayer
    setspeaker ("Ms. Tanaba")
	msgbox (format("Hi {PLAYER}, what should I get you?"))
	if(flag(FLAG_SYS_POKEDEX_GET) == TRUE) {pokemart (Orphanage_Mart_ExpandedItems)}
	else {pokemart(Orphanage_Mart_Basic)}
	msgbox(format("Play safe okay, child?"))
	release
	end
}

raw`
	.align 2
Orphanage_Mart_Basic:
	.2byte ITEM_POTION
	.2byte ITEM_ANTIDOTE
	.2byte ITEM_PARALYZE_HEAL
	.2byte ITEM_AWAKENING
	pokemartlistend

Orphanage_Mart_ExpandedItems::
	pokemart OldaleTown_Mart_Pokemart_Expanded
	msgbox gText_PleaseComeAgain, MSGBOX_DEFAULT
	release
	end

	.align 2
Orphanage_Mart_Expanded:
	.2byte ITEM_POKE_BALL
	.2byte ITEM_POTION
	.2byte ITEM_ANTIDOTE
	.2byte ITEM_PARALYZE_HEAL
	.2byte ITEM_AWAKENING
	pokemartlistend
`
script Blissey_Heal {
    lockall
    faceplayer
    playmoncry(SPECIES_BLISSEY, CRY_MODE_NORMAL)
    waitmoncry
    if (var(VAR_AKIHIKOS_INTRO_STATE) == 8){goto (BlisseyHealedPawmi)}
    elif (flag(FLAG_SYS_POKEMON_GET) == TRUE){
        special(HealPlayerParty)
        releaseall}
    else    
     {msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_NORMAL}
     Blissey is very good at healing. I can ask it for help when I have an injured Pokemon."))
        releaseall} 
}

script BlisseyHealedPawmi{
    lockall
    msgbox(format("{SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Thank you for healing Pawmi, Blissey!"))
    playmoncry(SPECIES_BLISSEY, CRY_MODE_NORMAL)
    applymovement(11, BlisseyHappy)
    waitmovement(0)
    releaseall
}
movement BlisseyHappy {
    walk_in_place_slow_down
    emote_music
}

script Minccino_NPC {
    lock
    faceplayer
    playmoncry(SPECIES_MINCCINO, CRY_MODE_NORMAL)
    release
}



script NicknamePartyMon {
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script NicknameBoxMon {
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    msgbox ("Transfered to PC.")
}

text NewLabTest_NotChosen {
    format("Pawmi looks at Akihiko expectantly.")
}


script SunshineOrphanageMainHall_Eventscript_MsKobayashiAskedToGoPickUpAPackage{
    lock
    playse(SE_PIN)
    applymovement (4, SunshineOrphanageMainHall_Eventscript_MsKobayashiMoveRight)
    waitmovement(0)
    msgbox(format("
    {SPEAKER NAME_KOBAYASHI}Good morning, Akihiko, Shinjiro.\p
    Sorry to trouble you but can you two go pick up a package for me?\p
    It should be delivered at the Pokecenter in Sunshine Town by now.\p
    You can get there by going through Sunshine Woods.\p
    You have a Pokémon now so it should be fine.\p
    {SPEAKER NAME_SHINJIRO}{CREATE_MUGSHOT MUGSHOT_SHINJIRO EMOTE_NORMAL}Of course, Ms. Kobayashi.\p
    {SPEAKER NAME_KOBAYASHI}{DESTROY_MUGSHOT}Here, you may need these."))
    closemessage
    giveitem (ITEM_POTION, 2)
    closemessage
    msgbox(format("
    {SPEAKER NAME_KOBAYASHI}Stay safe, dear.\p
    {SPEAKER NAME_AKIHIKO}{CREATE_MUGSHOT MUGSHOT_AKIHIKO EMOTE_SMILE}Thank you, Ms. Kobayashi!"))
    applymovement(13, walkup)
    waitmovement
    removeobject(13)
    setflag(FLAG_HIDE_SHINJIRO)
    setvar(VAR_AKIHIKOS_INTRO_STATE, 12)
    setrespawn(HEAL_LOCATION_SUNSHINE_ORPHANAGE_MAIN_HALL)
    release
}

movement SunshineOrphanageMainHall_Eventscript_MsKobayashiMoveRight {
    emote_exclamation_mark
    face_right
    delay_16
    walk_right *4
}

movement walkup {
    walk_up
}
